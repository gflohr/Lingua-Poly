#! /usr/bin/env perl
#
# Lingua-Poly   Language Disassembling Library
# Copyright (C) 2018-2019 Guido Flohr <guido.flohr@cantanea.com>
#               All rights reserved
#
# This library is free software. It comes without any warranty, to
# the extent permitted by applicable law. You can redistribute it
# and/or modify it under the terms of the Do What the Fuck You Want
# to Public License, Version 2, as published by Sam Hocevar. See
# http://www.wtfpl.net/ for more details.

use strict;

use Locale::PO;
use JSON;

sub save_json($$);

my @linguas = @ARGV or die "Usage: $0 LINGUAS\n";

my $en_po = '../src/assets/i18n/en-us.po';
my $entries = Locale::PO->load_file_asarray($en_po, 'utf-8')
    or die "$0: cannot open '$en_po' for reading: $!\n";

my %needed;
foreach my $entry(@$entries) {
    my $msgid = $entry->dequote($entry->msgid);
    next if '' eq $msgid;
    $msgid =~ s/\[\[([_a-zA-Z][_a-zA-Z0-9]*)\]\]/{$1}/g;
    $needed{$msgid} = 1;
}

foreach my $lingua (@linguas) {
    my %msgs;
    # Actually we should read the gmo file, not the po.
    my $entries = Locale::PO->load_file_asarray("$lingua.po", 'utf-8')
        or die "$0: cannot open '$lingua.po' for reading: $!\n";

    my %needed_here = %needed;
    foreach my $entry (@$entries) {
        my $msgid = $entry->dequote($entry->msgid);
        next if !delete $needed_here{$msgid};
        my $msgstr = $entry->dequote($entry->msgstr);
        $msgstr = $msgid if '' eq $msgstr;
        if ($entry->has_flag('perl-brace-format')) {
            $msgid =~ s/\{([_a-zA-Z][_a-zA-Z0-9]*)\}/[[$1]]/g;
            $msgstr =~ s/\{([_a-zA-Z][_a-zA-Z0-9]*)\}/{{$1}}/g;
        }
        $msgs{$msgid} = $msgstr;
    }

    my $json_file = "../src/assets/i18n/$lingua.json";
    save_json $json_file, \%msgs;
}

sub save_json($$) {
    my ($filename, $data) = @_;

    print "generating '$filename'.\n";
    open my $fh, '>:raw', $filename
        or die "cannot open '$filename' for writing: $!\n";
    $fh->print(encode_json $data)
        or die "cannot write to '$filename': $!\n";
    $fh->close or die "cannot close '$filename': $!\n";
}
